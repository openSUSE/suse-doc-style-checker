<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.github.com/openSUSE/suse-doc-style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:template match="/">
    <part>
      <part-title><xsl:apply-templates mode="part-title"/></part-title>
      <xsl:apply-templates/>
    </part>
  </xsl:template>

  <xsl:template match="*|db5:*" mode="part-title">Duplicated Words &amp; Phrases</xsl:template>

  <!-- The following template should probably match more elements. -->
  <xsl:template match="para|title|db5:para|db5:title">
    <xsl:if test="self::entry/para|self::db5:entry/db5:para">
      <xsl:apply-templates/>
    </xsl:if>
    <xsl:if test="not(ancestor-or-self::*/@role = 'legal') and
                  not(ancestor-or-self::db5:*/@role = 'legal')">
      <xsl:variable name="node" select="."/>
      <xsl:variable name="withinid">
        <xsl:call-template name="withinid-nomarkup"/>
      </xsl:variable>
      <xsl:variable name="file">
        <xsl:call-template name="file-nomarkup"/>
      </xsl:variable>
      <xsl:variable name="content-candidate">
        <xsl:apply-templates mode="dupes"/>
      </xsl:variable>
      <xsl:variable name="content-pretty-candidate">
        <xsl:apply-templates mode="content-pretty"/>
      </xsl:variable>
      <xsl:variable name="content"><xsl:value-of
        select="normalize-space($content-candidate)"/></xsl:variable>
      <xsl:variable name="content-pretty"><xsl:value-of
        select="normalize-space($content-pretty-candidate)"/></xsl:variable>

      <xsl:copy-of
        select="py:dupecheck($content, $content-pretty, $withinid, $file)"/>
    </xsl:if>
  </xsl:template>


  <xsl:template match="text()" mode="dupes">
    <xsl:value-of select="."/>
  </xsl:template>

  <xsl:template match="classname|code|command|computeroutput|constant|envar|
                       exceptionname|filename|function|interfacename|literal|
                       methodname|option|package|parameter|prompt|replaceable|
                       sgmltag|structfield|systemitem|tag|userinput|varname|
                       keycombo|keycap|menuchoice|guimenu|email|filename|ulink|
                       uri|xref|inlinemediaobject|symbol|
                       db5:classname|db5:code|db5:command|db5:computeroutput|
                       db5:constant|db5:envar|db5:exceptionname|db5:filename|
                       db5:function|db5:interfacename|db5:literal|db5:methodname|
                       db5:option|db5:package|db5:parameter|db5:prompt|
                       db5:replaceable|db5:sgmltag|db5:structfield|
                       db5:systemitem|db5:tag|db5:userinput|db5:varname|
                       db5:keycombo|db5:keycap|db5:menuchoice|db5:guimenu|
                       db5:email|db5:filename|db5:link|db5:uri|db5:xref|
                       db5:inlinemediaobject|db5:symbol"
    mode="dupes">
    ##@ignore##
  </xsl:template>

  <xsl:template match="remark|indexterm|db5:remark|db5:indexterm" mode="dupes"/>

  <xsl:template match="*|db5:*" mode="dupes">
    <xsl:apply-templates mode="dupes"/>
  </xsl:template>

</xsl:stylesheet>
