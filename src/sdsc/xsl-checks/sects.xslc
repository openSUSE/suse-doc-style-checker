<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.github.com/openSUSE/suse-doc-style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>


  <xsl:template match="*|db5:*" mode="part-title">Sections</xsl:template>

  <xsl:template match="article|chapter|reference|refentry|preface|appendix|colophon|
                       section|sect1|sect2|sect3|sect4|sect5|
                       db5:article|db5:chapter|db5:reference|db5:refentry|
                       db5:preface|db5:appendix|db5:colophon|db5:section|
                       db5:sect1|db5:sect2|db5:sect3|db5:sect4|db5:sect5">
    <xsl:variable name="lone">
      <xsl:choose>
        <xsl:when test="(local-name(.) = 'article' or
                         local-name(.) = 'chapter' or
                         local-name(.) = 'preface' or
                         local-name(.) = 'appendix' or
                         local-name(.) = 'colophon') and
                        (count(section) = 1 or count(sect1|db5:sect1) = 1)">1</xsl:when>
        <xsl:when test="local-name(.) = 'section' and count(section|db5:section) = 1">1</xsl:when>
        <xsl:when test="local-name(.) = 'sect1' and count(sect2|db5:sect2) = 1">1</xsl:when>
        <xsl:when test="local-name(.) = 'sect2' and count(sect3|db5:sect3) = 1">1</xsl:when>
        <xsl:when test="local-name(.) = 'sect3' and count(sect4|db5:sect4) = 1">1</xsl:when>
        <xsl:when test="local-name(.) = 'sect4' and count(sect5|db5:sect5) = 1">1</xsl:when>
        <xsl:otherwise>0</xsl:otherwise>
      </xsl:choose>
    </xsl:variable>


    <xsl:if test="not(para|abstract) and not(ancestor-or-self::*/@role = 'legal') and
                  not(db5:para|db5:abstract) and not(ancestor-or-self::db5:*/@role = 'legal')">
      <result type="error">
        <xsl:call-template name="sourcehint"/>
        <message>Section <xsl:call-template name="createid"/> lacks an
          introductory paragraph or abstract.</message>
        <suggestion>Add a <tag>para</tag> or <tag>abstract</tag>.</suggestion>
        <xsl:if test="local-name(.) = 'chapter' or local-name(.) = 'preface' or
                      local-name(.) = 'appendix' or local-name(.) = 'colophon' or
                      local-name(.) = 'section'">
          <suggestion>Use <tag>glossary</tag> or <tag>bibliography</tag> for
            glossaries or bibliographies, respectively. These elements do not
            need an introduction.
          </suggestion>
        </xsl:if>
      </result>
    </xsl:if>

    <xsl:if test="local-name(.) = 'sect4' or local-name(.) = 'sect5'">
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>Section <xsl:call-template name="createid"/> is nested deeply.
        </message>
        <suggestion>Avoid using <tag>sect4</tag> and <tag>sect5</tag> elements.
        </suggestion>
      </result>
    </xsl:if>

    <xsl:if test="$lone = 1">
      <result type="error">
        <xsl:call-template name="sourcehint"/>
        <message>Section <xsl:call-template name="createid"/> contains a lone
          subsection.
        </message>
        <suggestion>Flatten the subsection structure.</suggestion>
      </result>
    </xsl:if>

    <xsl:apply-templates/>
  </xsl:template>

</xsl:stylesheet>
