<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.github.com/openSUSE/suse-doc-style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>


  <xsl:template match="*|db5:*" mode="part-title">Identifiers</xsl:template>

  <xsl:template match="article[not(@id)]|chapter[not(@id)]|reference[not(@id)]|
                       refentry[not(@id)]|appendix[not(@id)]|colophon[not(@id)]|
                       section[not(@id)]|sect1[not(@id)]|sect2[not(@id)]|
                       sect3[not(@id)]|sect4[not(@id)]|sect5[not(@id)]|
                       db5:article[not(@xml:id)]|db5:chapter[not(@xml:id)]|
                       db5:reference[not(@xml:id)]|db5:refentry[not(@xml:id)]|
                       db5:appendix[not(@xml:id)]|db5:colophon[not(@xml:id)]|
                       db5:section[not(@xml:id)]|db5:sect1[not(@xml:id)]|
                       db5:sect2[not(@xml:id)]|db5:sect3[not(@xml:id)]|
                       db5:sect4[not(@xml:id)]|db5:sect5[not(@xml:id)]">
    <xsl:if test="not(ancestor::preface or ancestor-or-self::*/@role = 'legal')
              and not(ancestor::db5:preface or ancestor-or-self::db5:*/@role = 'legal')">
      <result type="error">
        <xsl:call-template name="sourcehint"/>
        <message>Section <xsl:call-template name="createid"/> has no identifier.
        </message>
        <suggestion>Add an <em>id</em> attribute.
        </suggestion>
      </result>
    </xsl:if>
  </xsl:template>

  <xsl:template match="*[@id]|db5:*[@xml:id]">
    <xsl:variable name="id" select="normalize-space(@id|@xml:id)"/>
    <xsl:variable name="id-safe">
      <xsl:call-template name="safecharacters">
        <xsl:with-param name="input" select="$id"/>
      </xsl:call-template>
    </xsl:variable>
    <xsl:variable name="correct-prefix">
      <xsl:choose>
        <xsl:when test="self::appendix or self::db5:appendix">app.</xsl:when>
        <xsl:when test="self::book or self::db5:book">book.</xsl:when>
        <xsl:when test="self::co or self::db5:co">co.</xsl:when>
        <xsl:when test="self::chapter or self::db5:chapter">cha.</xsl:when>
        <xsl:when test="self::example or self::db5:example">ex.</xsl:when>
        <xsl:when test="self::figure or self::db5:figure">fig.</xsl:when>
        <xsl:when test="self::glossary or self::glossterm or
                        self::db5:glossary or self::db5:glossterm">gl.</xsl:when>
        <xsl:when test="self::itemizedlist or self::db5:itemizedlist">il.</xsl:when>
        <xsl:when test="self::listitem or self::db5:listitem">li.</xsl:when>
        <xsl:when test="self::indexterm or self::db5:indexterm">idx.</xsl:when>
        <xsl:when test="self::orderedlist or self::db5:orderedlist">ol.</xsl:when>
        <xsl:when test="self::part or self::db5:part">part.</xsl:when>
        <xsl:when test="self::procedure or self::db5:procedure">pro.</xsl:when>
        <xsl:when test="self::qandaset or self::qandadiv or
                        self::qandaentry or
                        self::db5:qandaset or self::db5:qandadiv or
                        self::db5:qandaentry">qa.</xsl:when>
        <xsl:when test="self::sect1 or self::sect2 or self::sect3 or
                        self::sect4 or self::sect5 or self::section or
                        self::db5:sect1 or self::db5:sect2 or self::db5:sect3 or
                        self::db5:sect4 or self::db5:sect5 or
                        self::db5:section">sec.</xsl:when>
        <xsl:when test="self::set or self::db5:set">set.</xsl:when>
        <xsl:when test="self::step or self::db5:step">st.</xsl:when>
        <xsl:when test="self::table or self::db5:table">tab.</xsl:when>
        <xsl:when test="self::variablelist or self::db5:variablelist">vl.</xsl:when>
        <xsl:when test="self::varlistentry or self::db5:varlistentry">vle.</xsl:when>
        <xsl:otherwise>?</xsl:otherwise>
      </xsl:choose>
    </xsl:variable>


    <xsl:if test="not(starts-with($id, $correct-prefix)) and
                  not($correct-prefix = '?')">
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>Identifier of
          <xsl:call-template name="createid"/>
          has an unusual prefix.
        </message>
        <suggestion>Use the prefix
          <em><xsl:value-of select="$correct-prefix"/></em> instead.
        </suggestion>
      </result>
    </xsl:if>

    <xsl:if test="not(string-length($id) = string-length(normalize-space($id-safe)))">
      <xsl:variable name="id-safe-AZ">
        <xsl:call-template name="safecharacters-AZ">
          <xsl:with-param name="input" select="$id"/>
        </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="id-safe-special">
        <xsl:call-template name="safecharacters-special">
          <xsl:with-param name="input" select="$id"/>
        </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="id-safe-AZ-state"
        select="string-length($id) = string-length(normalize-space($id-safe-AZ))"/>
      <xsl:variable name="id-safe-special-state"
        select="string-length($id) = string-length(normalize-space($id-safe-special))"/>
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>Identifier
          <xsl:call-template name="createid"/>
          contains
          <xsl:if test="$id-safe-AZ-state = false">
            capital letters<xsl:choose><xsl:when test="$id-safe-special-state != false">.</xsl:when>
              <xsl:otherwise>
                and
              </xsl:otherwise>
            </xsl:choose>
          </xsl:if>
          <xsl:if test="$id-safe-special-state = false">
            special characters.
          </xsl:if>
        </message>
        <xsl:if test="$id-safe-AZ-state = false">
          <suggestion>Use only lowercase letters in identifiers.
          </suggestion>
        </xsl:if>
        <xsl:if test="$id-safe-special-state = false">
          <suggestion>Use only the following special characters in
            identifiers: ._-
          </suggestion>
        </xsl:if>
      </result>
    </xsl:if>
    <xsl:apply-templates/>
  </xsl:template>


</xsl:stylesheet>
