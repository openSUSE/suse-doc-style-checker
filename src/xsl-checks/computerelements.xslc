<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.gitorious.org/style-checker/style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:template match="*|db5:*" mode="part-title">Computer Elements</xsl:template>


  <xsl:template match="command|replaceable|option|literal|filename|
                       db5:command|db5:replaceable|db5:option|db5:literal|
                       db5:filename">
    <xsl:variable name="length" select="string-length(normalize-space(text()))"/>

    <xsl:if test="(self::command/parent::para or self::command/parent::title or
                  self::db5:command/parent::db5:para or
                  self::db5:command/parent::db5:title) and
                  normalize-space(following-sibling::node()[1]) = '' and
                  (local-name(following-sibling::node()[2]) = 'option' or
                   local-name(following-sibling::node()[2]) = 'replaceable')">
      <xsl:variable name="sibling" select="local-name(following-sibling::node()[2])"/>
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>Command <xsl:call-template name="createid"/>
          is directly followed by <tag><xsl:value-of select="$sibling"/></tag> element.
        </message>
        <suggestion>Move the <tag><xsl:value-of select="$sibling"/></tag> element within the command.
        </suggestion>
      </result>
    </xsl:if>

    <!-- Deliberately higher than the styleguide asks for. Try to avoid annoying
         people. -->
    <xsl:if test="$length &gt; 50">
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>
          <tag><xsl:value-of select="local-name(.)"/></tag> element
          <xsl:call-template name="createid"/>
          contains <xsl:value-of select="$length"/> characters.
        </message>
        <suggestion>Use a <tag>screen</tag> element.
        </suggestion>
      </result>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
