<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
  xmlns:db5="http://docbook.org/ns/docbook"
  xmlns:xlink="http://www.w3.org/1999/xlink"
  xmlns:py="https://www.gitorious.org/style-checker/style-checker"
  xmlns:exslt="http://exslt.org/common"
  exclude-result-prefixes="db5 xlink py exslt">
  <xsl:import href="library.xsl"/>

  <xsl:output method="xml" indent="yes" omit-xml-declaration="yes"/>

  <xsl:template match="*" mode="part-title">Figures</xsl:template>

  <xsl:template match="mediaobject|inlinemediaobject">

    <xsl:if test="not(textobject/phrase)">
      <result type="warning">
        <xsl:call-template name="sourcehint"/>
        <message>Media object
          <xsl:call-template name="createid">
            <xsl:with-param name="node" select="imageobject[1]/imagedata[1]"/>
            <xsl:with-param name="use-fileref-attribute" select="1"/>
          </xsl:call-template>
          does not contain alternative text.
        </message>
        <suggestion>Add a <tag>textobject</tag> element with a
          <tag>phrase</tag> inside it, then add descriptive text for the image
          to it.
        </suggestion>
      </result>
    </xsl:if>

    <xsl:apply-templates select="imageobject"/>
  </xsl:template>

<xsl:template match="imagedata[@fileref]">
    <xsl:variable name="fileref" select="normalize-space(@fileref)"/>
    <xsl:variable name="fileref-safe">
      <xsl:call-template name="safecharacters">
        <xsl:with-param name="input" select="$fileref"/>
      </xsl:call-template>
    </xsl:variable>

    <xsl:if test="not(string-length($fileref) = string-length(normalize-space($fileref-safe)))">
      <xsl:variable name="fileref-safe-AZ">
        <xsl:call-template name="safecharacters-AZ">
          <xsl:with-param name="input" select="$fileref"/>
        </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="fileref-safe-special">
        <xsl:call-template name="safecharacters-special">
          <xsl:with-param name="input" select="$fileref"/>
        </xsl:call-template>
      </xsl:variable>
      <xsl:variable name="fileref-safe-AZ-state"
        select="string-length($fileref) = string-length(normalize-space($fileref-safe-AZ))"/>
      <xsl:variable name="fileref-safe-special-state"
        select="string-length($fileref) = string-length(normalize-space($fileref-safe-special))"/>
      <result type="error">
        <xsl:call-template name="sourcehint"/>
        <message>Image file name
          <xsl:call-template name="createid">
            <xsl:with-param name="use-fileref-attribute" select="1"/>
          </xsl:call-template>
          contains
          <xsl:if test="$fileref-safe-AZ-state = false">
            capital letters<xsl:choose><xsl:when test="$fileref-safe-special-state != false">.</xsl:when>
              <xsl:otherwise>
                and
              </xsl:otherwise>
            </xsl:choose>
          </xsl:if>
          <xsl:if test="$fileref-safe-special-state = false">
            special characters.
          </xsl:if>
        </message>
        <xsl:if test="$fileref-safe-AZ-state = false">
          <suggestion>Use lowercase letters in image file names.
          </suggestion>
        </xsl:if>
        <xsl:if test="$fileref-safe-special-state = false">
          <suggestion>Use only the following special characters in image file
            names: ._-+
          </suggestion>
        </xsl:if>
      </result>
    </xsl:if>
  </xsl:template>

</xsl:stylesheet>
